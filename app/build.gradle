apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

ext.versionMajor = 0
ext.versionMinor = 4
ext.versionPatch = 6

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        applicationId "com.fenchtose.flutter"
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'OMDB_API_KEY', properties['omdb_api_key']
        buildConfigField 'String', 'RATINGS_ENDPOINT', properties['ratings_endpoint']
        buildConfigField 'String', 'RATINGS_API_KEY', properties['ratings_api_key']
    }
    buildFeatures {
        compose true
    }
    signingConfigs {
        release {
            def props = new Properties()
            def fileInputStream = new FileInputStream(file('../local.properties'))
            props.load(fileInputStream)
            fileInputStream.close()

            storeFile file('../flutter_key.jks')
            storePassword props['RELEASE_STORE_PASSWORD']
            keyAlias props['RELEASE_KEY_ALIAS']
            keyPassword props['RELEASE_KEY_PASSWORD']
        }
    }
    flavorDimensions "default"
    productFlavors {
        playstore {
            applicationId "com.fenchtose.flutter"
        }

        foss {
            applicationIdSuffix ".foss"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
    }
    afterEvaluate {
        android.sourceSets.all { sourceSet ->
            if (!sourceSet.name.startsWith("test"))
            {
                sourceSet.kotlin.setSrcDirs([])
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    composeOptions {
        kotlinCompilerExtensionVersion "$jetpack_compose_version"
    }
}

dependencies {
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation ('androidx.core:core-ktx:1.0.0', {
        exclude group: 'com.android.support'
    })

    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.3.1'
    // Animations
    implementation "androidx.compose.animation:animation:$jetpack_compose_version"
    // Tooling support (Previews, etc.)
    implementation("androidx.compose.ui:ui:$jetpack_compose_version")
    implementation("androidx.compose.ui:ui-tooling:$jetpack_compose_version")
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation("androidx.compose.foundation:foundation:$jetpack_compose_version")
    // Material Design
    implementation("androidx.compose.material:material:$jetpack_compose_version")
    // Material design icons
    implementation("androidx.compose.material:material-icons-core:$jetpack_compose_version")
    implementation("androidx.compose.material:material-icons-extended:$jetpack_compose_version")
    // Integration with observables
    implementation("androidx.compose.runtime:runtime-rxjava2:$jetpack_compose_version")
//    implementation "com.google.android.material:compose-theme-adapter:1.0.0"
    implementation "com.google.accompanist:accompanist-appcompat-theme:0.15.0"

    // Android TV
    //    implementation 'com.android.support:leanback-v17:25.3.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.0.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    debugImplementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    // Room
    implementation 'androidx.room:room-runtime:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.28.2'
    testImplementation ("com.nhaarman:mockito-kotlin-kt1.1:1.5.0", {
        exclude group: "org.jetbrains.kotlin"
    })
    testImplementation ("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version", {
        exclude group: "org.jetbrains.kotlin", module:"kotlin-stdlib"
    })

    // stetho
    debugImplementation 'com.facebook.stetho:stetho:1.5.1'
    debugImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // Leak canary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    playstoreImplementation 'com.android.billingclient:billing:3.0.0'
    playstoreImplementation 'com.google.firebase:firebase-analytics:17.2.0'

    // Moshi
    implementation 'com.squareup.moshi:moshi-kotlin:1.12.0'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.12.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    lintChecks project(":lintRules")
}

repositories {
    mavenCentral()
}

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}
